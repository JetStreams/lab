plugins {
    id "com.eriwen.gradle.css" version "2.14.0"
    id "com.eriwen.gradle.js" version "2.14.0"
    id "com.bmuschko.tomcat" version "2.5"
    id "org.bitbucket.grimrose.obake" version "0.6"
}

archivesBaseName = 'jetstreams'

ext {
    jsSrcDir = 'src/main/webapp/WEB-INF/views/res/js'
    env = 'dev'
}


dependencies {
    implementation project(':core')
    implementation 'org.springframework:spring-webmvc:4.0.6.RELEASE', \
            'commons-fileupload:commons-fileupload:1.3.1', \
            'net.sf.ehcache:ehcache-web:2.0.4'

    testImplementation 'org.mockito:mockito-core:1.9.5', 'org.springframework:spring-test:4.0.6.RELEASE'

    providedCompile 'javax.servlet:servlet-api:2.5'

    def tomcatVersion = '7.0.76'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

javascript.source {
    dev {
        js {
            srcDir jsSrcDir
            include "**/*.js"
            exclude "**/*.min.js"
        }
    }
    prod {
        js {
            srcDir jsSrcDir
            include "*.min.js"
        }
    }
}

combineJs {
    if (env == 'prod') {
        source = javascript.source.prod.js.files
    } else {
        source = javascript.source.dev.js.files
    }
    dest = file("${buildDir}/all.js")
}

minifyJs {
    source = combineJs
    dest = file("${buildDir}/all.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}
